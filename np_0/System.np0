MODULE System;

IMPORT Buffer;


CONST

  InfoOS* = 1;
  InfoMemStart* = 2; InfoMemSize* = 3;
  InfoStackStart* = 4; InfoHeapStart* = 5;
  InfoStdin* = 6; InfoStdout* = 7; InfoStderr* = 8;
  InfoArgc* = 9;

  ModeRead* = 1; ModeWrite* = 2;


TYPE

  OS* = RECORD
    os : LONGINT;
    pathSep* : CHAR;
    isWindows* : BOOLEAN
  END;

VAR

  os* : OS;
  heap : LONGINT;


PROCEDURE Halt*(nr : LONGINT);

  PROCEDURE Halt0(VAR nr : LONGINT);

    BEGIN
      SYS(1, nr, nr)
    END Halt0;

  BEGIN
    Halt0(nr)
  END Halt;


PROCEDURE Exec*(VAR x : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(2, x, buf)
  END Exec;


PROCEDURE GetInfo*(VAR x : LONGINT; nr : INTEGER);

  VAR nr2 : LONGINT;

  PROCEDURE GetInfo0(VAR x : LONGINT; VAR nr : LONGINT);
 
    BEGIN
      SYS(3, x, nr)
    END GetInfo0;

  BEGIN
    nr2 := nr;
    GetInfo0(x, nr2)
  END GetInfo;


PROCEDURE GetArg*(VAR len : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(4, len, buf)
  END GetArg;


PROCEDURE GetEnv*(VAR len : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(5, len, buf)
  END GetEnv;


PROCEDURE FileOpen*(VAR file : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(6, file, buf)
  END FileOpen;


PROCEDURE FileClose*(file : LONGINT);

  PROCEDURE FileClose0(VAR f : LONGINT);

    BEGIN
      SYS(7, f, f)
    END FileClose0;

  BEGIN
    FileClose0(file)
  END FileClose;


PROCEDURE FileRead*(VAR n : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(8, n, buf)
  END FileRead;


PROCEDURE FileWrite*(VAR n : LONGINT; VAR buf : Buffer.Rec);

  BEGIN
    SYS(9, n, buf)
  END FileWrite;


PROCEDURE HeapAlloc*(VAR addr : LONGINT; words : INTEGER);

  BEGIN
    ASSERT(words > 0);
    IF heap = -1 THEN
      GetInfo(heap, InfoHeapStart)
    END;
    addr := heap;
    heap := heap + words * 8
  END HeapAlloc;


BEGIN
  GetInfo(os.os, InfoOS);
  os.isWindows := os.os = 1;
  IF os.isWindows THEN os.pathSep := 5CX ELSE os.pathSep := 2FX END;
  heap := -1
END System.
